system Authorization {
    consumes ServiceRegistry;

    produces AuthorizationControl;
    produces TokenGeneration;
}

service AuthorizationControl {
    method VerifyIntraCloudAccess(IntraCloudAuthRequest): IntraCloudAuthResponse;
    method VerifyInterCloudAccess(InterCloudAuthRequest): InterCloudAuthResponse;
}

implement AuthorizationControl using HTTP/JSON {
    property BasePath: "/authorization";

    method VerifyIntraCloudAccess {
        Method: "PUT",
        Path: "/intracloud",
    }

    method VerifyInterCloudAccess {
        Method: "PUT",
        Path: "/intercloud",
    }
}

record IntraCloudAuthRequest {
    @Doc("
        The System which requested Service.
    ")
    Consumer: ArrowheadSystem,

    @Doc("
        A list of provider Systems who can provide Service.
    ")
    Providers: Set<ArrowheadSystem>,

    @Doc("
        The requested Service.
    ")
    Service: ArrowheadService,
}

record IntraCloudAuthResponse {
    AuthorizationState: Map<String, Boolean>,
}

record InterCloudAuthRequest {
    @Doc("
        The Cloud requesting a Service from the Local Cloud.
    ")
    Cloud: ArrowheadCloud,

    @Doc("
        The requested Service.
    ")
    Service: ArrowheadService,
}

record InterCloudAuthResponse {
    Authorized: Boolean,
}

service TokenGeneration {
    method Generate(TokenGenerationRequest): TokenGenerationResponse;
}

implement TokenGeneration using HTTP/JSON {
    property BasePath: "/authorization";

    method Generate {
        Method: "PUT",
        Path: "/token",
    }
}

record TokenGenerationRequest {
    @Doc("
        ArrowheadSystem that will try to access the Service instance.
    ")
    Consumer: ArrowheadSystem,

    @Doc("
        The ArrowheadCloud where the consumer System is located. If not
        defined, the Local Cloud is assumed.
    ")
    ConsumerCloud: ArrowheadCloud,

    @Doc("
        List of possible Application Systems that can provide the Service.
    ")
    Providers: List<ArrowheadSystem>,

    @Doc("
        The ArrowheadService that will be consumed.
    ")
    Service: ArrowheadService,

    @Doc("
        The duration until the Service exchange can happen.
    ")
    Duration: I32,

}

record TokenGenerationResponse {
    TokenData: List<TokenData>,
}

record TokenData {
    @Doc("
        ArrowheadSystem object of the Provider.
    ")
    System: ArrowheadSystem,

    @Doc("
        The ArrowheadService to be consumed.
    ")
    Service: ArrowheadService,

    @Doc("
        The actual token for that Provider-Consumer-Service combination.
    ")
    Token: String,

    @Doc("
        Message digest from the Authorization System.
    ")
    Signature: String,
}
