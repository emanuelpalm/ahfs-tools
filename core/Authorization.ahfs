system Authorization {
    consumes ServiceRegistry;

    produces AuthorizationControl;
    produces TokenGeneration;
}

service AuthorizationControl {
    interface IntraCloud {
        method VerifyAccess(IntraCloudAuthRequest): IntraCloudAuthResponse;
    }

    interface InterCloud {
        method VerifyAccess(InterCloudAuthRequest): InterCloudAuthResponse;
    }
}

implement AuthorizationControl using HTTP/JSON {
    property BasePath: "/authorization";

    interface IntraCloud {
        method VerifyAccess {
            Method: "PUT",
            Path: "/intracloud",
        }
    }

    interface InterCloud {
        method VerifyAccess {
            Method: "PUT",
            Path: "/intercloud",
        }
    }
}

record IntraCloudAuthRequest {
    /**
     * The System which requested Service.
     */
    Consumer: ArrowheadSystem,

    /**
     * A list of provider Systems who can provide Service.
     */
    Providers: Set<ArrowheadSystem>,

    /**
     * The requested Service.
     */
    Service: ArrowheadService,
}

record IntraCloudAuthResponse {
    AuthorizationState: Map<String, Boolean>,
}

record InterCloudAuthRequest {
    /**
     * The Cloud requesting a Service from the Local Cloud.
     */
    Cloud: ArrowheadCloud,

    /**
     * The requested Service.
     */
    Service: ArrowheadService,
}

record InterCloudAuthResponse {
    Authorized: Boolean,
}

service TokenGeneration {
    interface Generation {
        method Generate(TokenGenerationRequest): TokenGenerationResponse;
    }
}

implement TokenGeneration using HTTP/JSON {
    property BasePath: "/authorization";

    interface Generation {
        method Generate {
            Method: "PUT",
            Path: "/token",
        }
    }
}

record TokenGenerationRequest {
    /**
     * ArrowheadSystem that will try to access the Service instance.
     */
    Consumer: ArrowheadSystem,

    /**
     * The ArrowheadCloud where the consumer System is located. If not defined,
     * the Local Cloud is assumed.
     */
    ConsumerCloud: ArrowheadCloud,

    /**
     * List of possible Application Systems that can provide the Service.
     */
    Providers: List<ArrowheadSystem>,

    /**
     * The ArrowheadService that will be consumed.
     */
    Service: ArrowheadService,

    /**
     * The duration until the Service exchange can happen.
     */
    Duration: i32,

}

record TokenGenerationResponse {
    TokenData: List<TokenData>,
}

record TokenData {
    /**
     * ArrowheadSystem object of the Provider.
     */
    System: ArrowheadSystem,

    /**
     * The ArrowheadService to be consumed.
     */
    Service: ArrowheadService,

    /**
     * The actual token for that Provider-Consumer-Service combination.
     */
    Token: String,

    /**
     * Message digest from the Authorization System.
     */
    Signature: String,
}
